plugins {
    id 'org.springframework.boot' version '3.4.3'
    id 'io.spring.dependency-management' version '1.1.7'
    id 'java'
}

group = 'net.windwards.spring'
version = '0.0.1-SNAPSHOT'

java.sourceCompatibility = '21'

bootRun {
    environment SPRING_PROFILES_ACTIVE: environment.SPRING_PROFILES_ACTIVE ?: 'local'
}

repositories {
    mavenCentral()
}

dependencies {
    implementation('org.springframework.boot:spring-boot-starter-web')
    if (System.properties['webserver'] == 'jetty') {
        implementation('org.springframework.boot:spring-boot-starter-jetty')
        modules {
            module("org.springframework.boot:spring-boot-starter-tomcat") {
                replacedBy("org.springframework.boot:spring-boot-starter-jetty", "Use Jetty")
            }
        }
    } else if (System.properties['webserver'] == 'undertow') {
        implementation('org.springframework.boot:spring-boot-starter-undertow')
        modules {
            module("org.springframework.boot:spring-boot-starter-tomcat") {
                replacedBy("org.springframework.boot:spring-boot-starter-undertow", "Use Undertow")
            }
        }
    }
    implementation('org.springframework.boot:spring-boot-starter-validation')
    developmentOnly('org.springframework.boot:spring-boot-devtools')
    testImplementation('org.springframework.boot:spring-boot-starter-test')
}

tasks.withType(Test) {
    useJUnitPlatform()
}
